/*
 * Copyright 2010 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.drools.examples.honestpolitician
 
import org.drools.examples.honestpolitician.Politician;
import org.drools.examples.honestpolitician.Hope;
 
rule "We have an honest Politician"   //1
    salience 10
    when
        exists( Politician( honest == true ) )
    then
    	System.out.println("we have an honest politician rule was fired");
        insertLogical( new Hope() );
end

rule "Hope Lives"  //2
    salience 10
    when
        exists( Hope() )
    then
        System.out.println("Hurrah!!! Democracy Lives");
end

rule "Hope is Dead"  //3
    when
        not( Hope() )
    then
        System.out.println( "We are all Doomed!!! Democracy is Dead" );
end

rule "Corrupt the Honest"  //4
    when
        politician : Politician( honest == true )   
        exists( Hope() )
    then
        System.out.println( "I'm an evil corporation and I have corrupted " + politician.getName() );
        modify( politician ) { 
            setHonest( false ) 
        }
end
// First politician inserted->rules 1&3 activate, but rule 1 will fire
// 
